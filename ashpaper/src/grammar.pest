
program = {
    comment | instruction | noop
}

instruction = _{
    ( register1 | register0)  ~
    (goto
    | negate
    | multiply
    | add
    | print_char
    | print_value
    | pop
    | push
    | store_syllables)
}

register1 = { start ~ whitespace+ }
register0 = { start }

goto  = { any_not_forward_slash* ~ "/" ~ any* ~ end }
negate = { any_not_caps_in_word* ~ caps_in_word ~ any* ~ end }
multiply = { (uppercase_letter | (any_not_title_case* ~ title_case)) ~ any* ~ end }
add = { like_lit | as_lit }
print_char =  { any_not_question* ~ question ~ any* ~ end }
print_value =  { any_not_dot* ~ dot ~ any* ~ end }
pop =  { any_not_comma* ~ comma ~ any* ~ end }
push =  { any_not_hyphen* ~ hyphen ~ any* ~ end }
noop = { start ~ end }
store_syllables =  { any+ }

forward_slash = _{ "/" }
any_not_forward_slash = _{ !forward_slash ~ any }

caps_in_word = _{ character ~ uppercase_letter}
any_not_caps_in_word = _{ !caps_in_word ~ any }

title_case = _{ whitespace ~ uppercase_letter }
any_not_title_case = _{ !title_case ~ any }

like_lit = _{ any_not_like* ~ "like" ~ any* ~ end }
any_not_like = _{ !"like" ~ any }

as_lit = _{ any_not_as* ~ "as" ~ any* ~ end }
any_not_as = _{ !"as" ~ any }

question = _{ "?" }
any_not_question = _{ !question ~ any }

dot = _{ "." }
any_not_dot = _{ !dot ~ any }

comma = _{ "," }
any_not_comma = _{ !comma ~ any }


hyphen = _{ "-" }
any_not_hyphen = _{ !hyphen ~ any }

start = _{ SOI }

end = _{ whitespace* ~ EOI }

comment = { start ~ whitespace* ~ ";;" ~ any* ~ end }

character = _{ ASCII_ALPHANUMERIC | "." | "_" | "/" }
any_not_character = _{ !character ~ any }

uppercase_letter = _{ UPPERCASE_LETTER }

whitespace = _{ " " | "\t" }
any = _{ ANY }
